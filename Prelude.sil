CT = {{0,0},{0,0}}

and : {0,{0,0}} = \a b -> if a then b else 0

or : {0,{0,0}} = \a b -> if a then 1 else b

not : {0,0} = \x -> if x then 0 else 1

succ : {0,0} = \x -> {x,0}

plus : {CT,{CT,CT}} = \m n f x -> m f (n f x)

times : {CT,{CT,CT}} = \m n f -> m (n f)

pow : {{CT,CT},{CT,CT}} = \m n -> m n

makeGT : {0,0} = \FT -> {{{FT,FT},{FT,FT}}, {{FT,FT},{FT,FT}}}

mapG : makeGT {{0,0},{0,0}} = \r f -> let layer = \recur l -> {f (left l), recur (right l)}
                                          base = \l -> 0
                                      in r layer base
map = mapG $255

foldrT = makeGT {{0,{0,0}},{0,{0,0}}}
foldrG : foldrT = \r f -> let layer = \recur accum l -> if l
                                                          then recur (f (left l) accum) (right l)
                                                          else accum
                              base = \accum l -> accum
                          in r layer base 
foldr = foldrG $255

zipWithT = makeGT {{0,{0,0}},{0,{0,0}}}
zipWithG : zipWithT = \r f -> let layer = \recur a b -> if (and a b)
                                                          then {f (left a) (left b)
                                                                , recur (right a) (right b)}
                                                          else 0
                                  base = \a b -> 0
                              in r layer base 
zipWith = zipWithG $255

d2c = let layer = \recur i f b -> if i
                                     then f (recur (left i) f b)
                                     else b
          fixer : {{{0,CT},{0,CT}},{{0,CT},{0,CT}}} = $1024
      in fixer layer (\i f b -> 0)

c2d : {CT,0} = \c -> c succ 0

dEqual : {0,{0,0}} = \a b -> let equalsOne : {0,0} = \x -> if x then (not (left x)) else 0
                             in if a then equalsOne (d2c (left a) (\x -> left x) b)
                                     else not b

listLength = foldr (\a b -> succ b) 0

listEqual : {0,{0,0}} = \a b -> let pairsEqual = zipWith dEqual a b
                                    lengthEqual = dEqual (listLength a) (listLength b)
                                in foldr and 1 {lengthEqual,pairsEqual}
